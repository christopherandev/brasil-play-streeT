YCMD:spawn(playerid, params[], help)
{
    if(!GetFlag(Player[playerid][pyr::flags], FLAG_PLAYER_LOGGED)) return 1;

    if(GetFlag(Player[playerid][pyr::flags], FLAG_PLAYER_INJURED) || GetFlag(Player[playerid][pyr::flags], FLAG_PLAYER_RESUSCITATION))
        return SendClientMessage(playerid, -1, "{ff5533}[ BPS ] {ffffff}Você não pode usar /spawn enquanto estiver ferido ou reavivando.");

    Player::Spawn(playerid);
    SendClientMessage(playerid, -1, "{99ff99}[ BPS ] {ffffff}Você foi enviado ao spawn!");
    return 1;
}

YCMD:rev(playerid, params[], help)
{
    #pragma unused params, help

    if(!GetFlag(Player[playerid][pyr::flags], FLAG_PLAYER_LOGGED)) return 1;

    if(!Player::HandleResuscitationAction(playerid))
        SendClientMessage(playerid, -1, "{ff5533}[ SOCORRO ] {ffffff}Não há jogadores feridos próximos para reavivar.");

    return 1;
}

YCMD:dance(playerid, params[], help)
{
    if(!GetFlag(Player[playerid][pyr::flags], FLAG_PLAYER_LOGGED)) return 1;

    new danceid;

    if(sscanf(params, "i", danceid))
        return SendClientMessage(playerid, -1, "{ff3333}[ CMD ] {ffffff}Use: /dance {ff3333}[ ID ]");
    
    switch(danceid)
    {
        case 0:     ClearAnimations(playerid);
        case 1:     ApplyAnimation(playerid, "DANCING", "dnce_M_d", 4.1, true, false, false, false, 0, SYNC_ALL);
        case 2:     ApplyAnimation(playerid, "DANCING", "dnce_M_c", 4.1, true, false, false, false, 0, SYNC_ALL);
        default:    ApplyAnimation(playerid, "DANCING", "dance_loop", 4.1, true, false, false, false, 0, SYNC_ALL);
    }
    return 1;
}

YCMD:skin(playerid, params[], help)
{
    if(!GetFlag(Player[playerid][pyr::flags], FLAG_PLAYER_LOGGED)) return 1;

    new skinid;

    if(sscanf(params, "i", skinid))
        return SendClientMessage(playerid, -1, "{ff3333}[ CMD ] {ffffff}Use: /skin {ff3333}[ SKINID ]");
    
    if(skinid < 0 || skinid > 311) 
        return SendClientMessage(playerid, -1, "{ff3333}[ CMD ] {ffffff}Erro do parâmetro: {ff3333}skinid {ffffff}[0 - 311]");
    
    SetPlayerSkin(playerid, skinid);

    
    DB::SetDataInt(db_entity, "players", "skinid", skinid, "name = '%q'", GetPlayerNameStr(playerid));

    SendClientMessage(playerid, -1, "{99ff99}[ SUCESSO ] {ffffff}Sua skin foi alterada com {99ff99}sucesso!");

    return 1;
}

YCMD:ajuda(playerid, params[], help)
{
    if(!GetFlag(Player[playerid][pyr::flags], FLAG_PLAYER_LOGGED)) return 1;
    
    return 1;
}

YCMD:acessorios(playerid, params[], help)
{
    if(!GetFlag(Player[playerid][pyr::flags], FLAG_PLAYER_LOGGED)) return 1;
    
    if(GetFlag(game::Player[playerid][pyr::flags], FLAG_PLAYER_INGAME) && !GetFlag(game::Player[playerid][pyr::flags], FLAG_PLAYER_FINISHED))
        return SendClientMessage(playerid, -1, "{ff5533}[ ERRO ] {ffffff}Você não pode mexer com acessórios durante o evento!");

    acs::ClearData(playerid);

    new msg[1024], name[MAX_PLAYER_NAME], modelid;
    
    GetPlayerName(playerid, name);

    format(msg, sizeof(msg), "{ffffff}Slot\t{ffffff}ID \t{ffffff}Status\n");

    for(new i = 0; i < MAX_PLAYER_ACESSORYS; i++)
    {
        if(DB::Exists(db_entity, "acessorys", "owner = '%q' AND slotid = %d", name, i))
        {
            DB::GetDataInt(db_entity, "acessorys", "modelid", modelid, "owner = '%q' AND slotid = %d", name, i);

            format(msg, sizeof(msg), "%s{ffffff}Slot: {9999ff}%d\t{ffffff}%d\t%s\n", 
            msg, i + 1, modelid,
            IsPlayerAttachedObjectSlotUsed(playerid, i) ? "{33ff33}[ EQUIPADO ]" : "{ff9933}[ DESEQUIPADO ]");
        }

        else
            format(msg, sizeof(msg), "%s{ffffff}Slot: {ff3333}%d\t\t{ff3333}[ VAZIO ]\n", msg, i + 1);
    }

    format(msg, sizeof(msg), "%s{cdcdcd}CLIQUE AQUI para comprar\t\t\n", msg);
    
    Dialog_ShowCallback(playerid, using public Response_ACC_MENU<iiiis>, DIALOG_STYLE_TABLIST_HEADERS, "{FFFFFF}Gerenciar Acessorios", msg, "Selecionar", "Fechar");
    return 1;
}

YCMD:orgs(playerid, params[], help)
{
    if(help)
    {
        SendClientMessage(playerid, -1, "{ffff33}[ AJUDA ORG ] {ffffff}Veja as organizações atuais e o número de membros online.");
        return 1;
    }

    if(!GetFlag(Player[playerid][pyr::flags], FLAG_PLAYER_LOGGED)) return 1;

    new msg[1024], line[128], org::members[MAX_ORGS];
    
    foreach(new i : Player)
    {
        if(!IsValidPlayer(i) || org::Player[i][pyr::orgid] == INVALID_ORG_ID) continue;
        org::members[org::Player[i][pyr::orgid]]++;
    }

    strcat(msg, "{ffffff}Organizacao\t{ff99ff}Tipo\t{ffffff}Lider\t{ff99ff}Membros Online\n");

    for(new i = 0; i < MAX_ORGS; i++)
    {
        if(!GetFlag(Org[i][org::flags], FLAG_ORG_CREATED)) continue;
        
        format(line, 128, "{%06x}%s\t%s\t%s\t{99ff99}%d {ffffff}membros\n", Org[i][org::color] >>> 8, Org[i][org::name], 
        Org::gTypeNames[_:Org[i][org::type]], Org[i][org::leader], org::members[i]);
        
        strcat(msg, line);
    }

    inline no_use_dialog(playerid1, dialogid, response, listitem, string:inputtext[])
    {
        #pragma unused playerid1, dialogid, response, listitem, inputtext

        return 1;
    }
    
    Dialog_ShowCallback(playerid, using inline no_use_dialog, DIALOG_STYLE_TABLIST_HEADERS, "Orgs do Servidor", msg, "Fechar");
   
    return 1;
}
