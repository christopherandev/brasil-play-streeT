

stock bool:Race::IsRaceVehicle(vehicleid)
{
    for(new gid = 0; gid < MAX_GAMES_INSTANCES; gid++)
    {
        if(!GetFlag(Game[gid][game::flags], FLAG_GAME_CREATED)) continue;

        if(Game[gid][game::type] != GAME_TYPE_RACE) continue;
        
        if(!map_valid(Race[gid][race::participant])) continue;

        new len = Game::GetPlayersCount(gid);
        for(new i = 0; i < len; i++)
        {
            new pid = list_get(Game[gid][game::players], i);
            if(!map_has_key(Race[gid][race::participant], pid)) continue;

            new data[E_RACE_SEAT];
            map_get_arr(Race[gid][race::participant], pid, data);

            if(data[race::vehicleid] != vehicleid) continue;

            return true;
        }
    }

    return false;
}

stock Race::ProtectVehicle(vehicleid)
{
    if(!IsValidVehicle(vehicleid)) return 0;

    new Float:health;
    GetVehicleHealth(vehicleid, health);

    if(health < 2000.0)
        SetVehicleHealth(vehicleid, 2000.0);

    new Float:qw, Float:qx, Float:qy, Float:qz;
    GetVehicleRotationQuat(vehicleid, qw, qx, qy, qz);

    // m33 < 0: eixo "up" invertido -> veículo tombado/capotado.
    new Float:m33 = 1.0 - 2.0 * (qx * qx + qy * qy);
    if(m33 < 0.0)
    {
        new Float:x, Float:y, Float:z, Float:a;
        GetVehiclePos(vehicleid, x, y, z);
        GetVehicleZAngle(vehicleid, a);

        SetVehiclePos(vehicleid, x, y, z + 0.15);
        SetVehicleZAngle(vehicleid, a);
        SetVehicleHealth(vehicleid, 2000.0);
    }

    return 1;
}

stock Race::Create(raceid, const args[])
{
    if(sscanf(args, "iia<f>["#MAX_GAME_PARTICIPANTS"]", Race[raceid][race::modelid], Race[raceid][race::lap], Race[raceid][race::rewards]))
    {
        printf("[ EVENTO ] Houve erro de argumento no sscanf ao tentar criar evento de corrida");
        return 0;
    }    

    Race[raceid][race::flags]      |= FLAG_GAME_CREATED;
    Race[raceid][race::podium]      = list_new();
    Race[raceid][race::finisheds]   = 0;
    Race[raceid][race::countpart]   = 0;
    Race[raceid][race::participant] = map_new();

    return 1;
}

stock Race::Destroy(raceid)
{
    if(!list_valid(Race[raceid][race::podium]))      return 0;
    if(!map_valid(Race[raceid][race::participant]))  return 0;

    list_delete(Race[raceid][race::podium]);
    map_delete(Race[raceid][race::participant]);

    Race[raceid][race::modelid]   = 0;

    for(new i = 0; i < MAX_GAME_PARTICIPANTS; i++)
        Race[raceid][race::rewards][i] = 0.0;

    Race[raceid][race::lap]       = 0;
    Race[raceid][race::flags]     = 0;
    Race[raceid][race::finisheds] = 0;
    Race[raceid][race::countpart] = 0;

    return 1;
}

stock Race::SendPlayer(playerid, raceid)
{
    SetPlayerPos(playerid, -1403.0116 + RandomFloatMinMax(-2.0, 2.0), -250.4526 + RandomFloatMinMax(-2.0, 2.0), 1043.5341);
    SetPlayerInterior(playerid, Race::gInteriorID);
    SetPlayerVirtualWorld(playerid, Game[raceid][game::vw]);

    new data[E_RACE_SEAT];

    data[race::laps]      = 1;
    data[race::checkid]   = 0;
    data[race::vehicleid] = INVALID_VEHICLE_ID;

    map_add_arr(Race[raceid][race::participant], playerid, data);

    SendClientMessage(playerid, -1, "{3399ff}[ EVENTO ] {ffffff}Você entrou na corrida {3399ff}%s.", Game[raceid][game::name]);

    return 1;
}

stock Race::QuitPlayer(raceid, playerid)
{
    if(!list_valid(Race[raceid][race::podium]))      return 0;
    if(!map_valid(Race[raceid][race::participant]))  return 0;
    
    new data[E_RACE_SEAT];
    if(map_has_key(Race[raceid][race::participant], playerid))
    {
        map_get_arr(Race[raceid][race::participant], playerid, data);
        if(IsValidVehicle(data[race::vehicleid]))
            Veh::Destroy(data[race::vehicleid]);

        map_remove(Race[raceid][race::participant], playerid);
    }

    if(GetFlag(game::Player[playerid][pyr::flags], FLAG_PLAYER_ELIMINATED))
        Race::RemPodium(playerid, raceid);
    
    if(!IsPlayerConnected(playerid)) return 1;
    
    if(IsPlayerInAnyVehicle(playerid)) RemovePlayerFromVehicle(playerid);
    PlayerPlaySound(playerid, 31202, 0.0, 0.0, 0.0);
    DisablePlayerCheckpoint(playerid);
    SetPlayerInterior(playerid, 0);
    SetPlayerVirtualWorld(playerid, 0);
    Player::Spawn(playerid); 

    ResetFlag(game::Player[playerid][pyr::flags], FLAG_PLAYER_INGAME);
    
    return 1;
}

stock Race::Ready(raceid)
{
    new len = Game::GetPlayersCount(raceid);

    new data[E_RACE_SEAT];

    for(new i = 0; i < len; i++)
    {
        new playerid = list_get(Game[raceid][game::players], i);
     
        if(!IsValidPlayer(playerid)) continue;

        map_get_arr(Race[raceid][race::participant], playerid, data);

        new veh_data[E_VEHICLES];

        veh_data[veh::pX]           = Race::gVehicleSpawns[i][0];
        veh_data[veh::pY]           = Race::gVehicleSpawns[i][1];
        veh_data[veh::pZ]           = Race::gVehicleSpawns[i][2];
        veh_data[veh::pA]           = Race::gVehicleSpawns[i][3];
        veh_data[veh::fuel]         = 60.0;
        veh_data[veh::health]       = 2000.0;
        veh_data[veh::interiorid]   = Race::gInteriorID;
        veh_data[veh::worldid]      = Game[raceid][game::vw];
        veh_data[veh::params]       = FLAG_PARAM_ENGINE;
        veh_data[veh::color1]       = RandomMinMax(0, 255);
        veh_data[veh::color2]       = RandomMinMax(0, 255);

        /* SET VEHICLE */
        data[race::vehicleid] = Veh::Create(veh_data);
        
        SetVehicleHealth(data[race::vehicleid], 1000.0 + 1000.0);
       
        /* SET PLAYER */
        map_set_arr(Race[raceid][race::participant], playerid, data);
        ResetFlag(game::Player[playerid][pyr::flags], FLAG_PLAYER_WAITING);
        SetFlag(game::Player[playerid][pyr::flags], FLAG_PLAYER_PLAYING);
        
        SetPVarInt(playerid, "PutPlayerInVehicle", 1);

        SetPlayerPos(playerid, Race::gVehicleSpawns[i][0], Race::gVehicleSpawns[i][1], Race::gVehicleSpawns[i][2]);
        SetPlayerInterior(playerid, GetVehicleInterior(data[race::vehicleid]));
        SetPlayerVirtualWorld(playerid, GetVehicleVirtualWorld(data[race::vehicleid]));      
        
        Race::AddPodium(playerid, raceid);
        Race[raceid][race::countpart]++;

        TogglePlayerControllable(playerid, false);
    }
    
    Game::SendMessageToAll(raceid, "{ff9933}[ EVENTO ] {ffffff}Iniciando Corrida! {ff9933}Se preparem!");
    
    return 1;
}

stock Race::Start(raceid, tick, &new_tick)
{
    new len = Race::GetPlayersCount(raceid);

    if(tick <= 0)
    {
        for(new i = 0; i < len; i++)
        {
            new playerid = list_get(Race[raceid][race::podium], i);
            
            if(!IsValidPlayer(playerid)) continue;

            new data[E_RACE_SEAT];
            map_get_arr(Race[raceid][race::participant], playerid, data);   

            Race::UpdatePlayerCheck(playerid, data);
            TogglePlayerControllable(playerid, true);
        }

        Game::ShowTextForAll(raceid, "~r~VAI...", 1000, 3);
        Game::PlaySoundForAll(raceid, 1057);
        new_tick = 250; 
    }

    else
    {
        Game::ShowTextForAll(raceid, "~r~~h~%d", 990, 3, tick);
        Game::PlaySoundForAll(raceid, 1056);
    }

    return 1;
}

stock Race::GetPlayersCount(raceid)
{
    if(!list_valid(Race[raceid][race::podium])) return 0;
    return list_size(Race[raceid][race::podium]);
}

stock Race::Update(raceid, tick)
{       
    if(!list_valid(Race[raceid][race::podium])) return 0;
  
    new count = list_size(Race[raceid][race::podium]);

    if(count <= 0 || tick <= 0 || Race[raceid][race::finisheds] == count)
    {
        if(Race[raceid][race::finisheds] == 0)
            SendClientMessageToAll(-1, "{ff3333}[ CORRIDA ] {ffffff}Todos desistiram da corrida. {ff3333}Não houve vencedores.");
        else
            SendClientMessageToAll(-1, "{3399ff}[ CORRIDA ] {ffffff}A corrida {3399ff}%s {ffffff}acabou!", Game[raceid][game::name]);
    
        Game[raceid][game::tick] = 0;
        return 1;
    }

    if(tick <= 60)
    {
        if(tick == 60)
            Game::SendMessageToAll(raceid, "{ff9933}[ EVENTO ] {ffffff}A corrida vai termina em {ff9933}1 minuto!");
       
        Game::ShowTextForAll(raceid, "~r~%02d~w~:~r~%02d", (tick <= 10) ? 600 : 990, 4, floatround(tick/60), tick % 60);
    }
 
    for(new i = 0; i < list_size(Race[raceid][race::podium]); i++)
    {
        new playerid = list_get(Race[raceid][race::podium], i);
        
        if(!map_has_key(Race[raceid][race::participant], playerid)) continue;
  
        new data[E_RACE_SEAT];
        map_get_arr(Race[raceid][race::participant], playerid, data);
        Race::ProtectVehicle(data[race::vehicleid]);
    }

    Race::UpdatePodium(raceid);
    return 1;
}

stock Race::Finish(raceid)
{
    SetFlag(Race[raceid][race::flags], FLAG_RACE_FINISHED);

    new len = list_size(Race[raceid][race::podium]);

    for(new i = len - 1; i >= 0; i--)
    {
        new playerid = list_get(Race[raceid][race::podium], i);
        
        if(!IsValidPlayer(playerid)) continue;
        
        if(GetFlag(game::Player[playerid][pyr::flags], FLAG_PLAYER_FINISHED))
        {
            
            new place = Race::GetPodiumPlace(raceid, playerid) - 1;
            new Float:reward = (place >= 0 && place < MAX_GAME_PARTICIPANTS) ? Race[raceid][race::rewards][place] : 0.0;
        
            if(reward > 0.0)
            {
                SendClientMessage(playerid, -1, "{3399ff}[ CORRIDA ] {ffffff}Premiação da corrida: {3399ff}R$ %.2f{ffffff} por terminar em {3399ff}%d {ffffff} | lugar.", reward, i + 1);
                Player::GiveMoney(playerid, reward);
            }

            else
                SendClientMessage(playerid, -1, "{ff3333}[ CORRIDA ] {ffffff}Você {ff3333}não se classificou {ffffff}para receber recompensa!");

        }
        
        else
        {
            ResetFlag(game::Player[playerid][pyr::flags], FLAG_PLAYER_PLAYING);
            SetFlag(game::Player[playerid][pyr::flags], FLAG_PLAYER_ELIMINATED);
            Game::RemovePlayer(raceid, playerid);
        }
    }

    return 1;
}

stock GetPlayerRaceProgress(playerid, raceid)
{
    if(!map_has_key(Race[raceid][race::participant], playerid)) return 1;

    new data[E_RACE_SEAT];
    map_get_arr(Race[raceid][race::participant], playerid, data);   
    return (data[race::laps] * sizeof(Race::gCheckpoints)) + data[race::checkid];
}

stock Race::UpdatePodium(raceid)
{
    new len = Race::GetPlayersCount(raceid);

    if(len <= 1) return 1;

    for(new i = 1; i < len; i++)
    {
        new playerid = list_get(Race[raceid][race::podium], i);

        if(!IsValidPlayer(playerid)) continue;

        new 
            player_progress = GetPlayerRaceProgress(playerid, raceid),
            j = i - 1
        ;

        while(j >= 0)
        {
            new otherid = list_get(Race[raceid][race::podium], j);

            if(GetFlag(game::Player[otherid][pyr::flags], FLAG_PLAYER_FINISHED)) break;
        
            new other_progress = GetPlayerRaceProgress(otherid, raceid);

            if(player_progress <= other_progress) break;

            list_set(Race[raceid][race::podium], j + 1, otherid);
            list_set(Race[raceid][race::podium], j, playerid);

            Game::SendMessageToAll(
                raceid,
                "{3399ff}[ CORRIDA ] {ffffff}O corredor {3399ff}%s {ffffff}ultrapassou {3399ff}%s",
                GetPlayerNameStr(playerid),
                GetPlayerNameStr(otherid)
            );

            j--;
        }
    }

    return 1;
}

stock Race::AddPodium(playerid, raceid)
{
    if(!list_valid(Race[raceid][race::podium])) return 0;

    if(list_find(Race[raceid][race::podium], playerid) == -1)
    {
        list_add(Race[raceid][race::podium], playerid);
        return 1;
    }

    return 0;
}

stock Race::RemPodium(playerid, raceid)
{
    if(!list_valid(Race[raceid][race::podium])) return 0;

    new idx = list_find(Race[raceid][race::podium], playerid);

    if(idx != -1)
    {
        list_remove(Race[raceid][race::podium], idx);      
        return 1;
    }

    return 0;
}

stock Race::GetPodiumPlace(raceid, playerid)
{
    if(!list_valid(Race[raceid][race::podium])) return 0;
    return (list_find(Race[raceid][race::podium], playerid) + 1);
}

stock Race::UpdatePlayerCheck(playerid, data[E_RACE_SEAT])
{
    new raceid = game::Player[playerid][pyr::gameid];

    SetPlayerCheckpoint(playerid,
    Race::gCheckpoints[data[race::checkid]][0], 
    Race::gCheckpoints[data[race::checkid]][1], 
    Race::gCheckpoints[data[race::checkid]][2], 25.0);

    map_set_arr(Race[raceid][race::participant], playerid, data);

    SendClientMessage(playerid, -1, 
    "{3399ff}[ CORRIDA ] {ffffff}Checkpoint {3399ff}%d/%d {ffffff}| Volta: {3399ff}%d/%d {ffffff}| Posição: {3399ff}%d| Lugar",
    data[race::checkid] + 1, sizeof(Race::gCheckpoints), data[race::laps], Race[raceid][race::lap], Race::GetPodiumPlace(raceid, playerid));

    return 1;
}
